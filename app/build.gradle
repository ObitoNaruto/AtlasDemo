group = 'com.taobao.android.atlasdemo'
version = getEnvValue("versionName", "1.0.0");
def apVersion = getEnvValue("apVersion", "");//打包patch配置时用到

//应用plugin
apply plugin: 'com.android.application'
apply plugin: 'com.taobao.atlas'

repositories {
    mavenLocal()
    jcenter()
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "com.taobao.demo.wrapper2"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName version
        vectorDrawables.useSupportLibrary = true
    }

    dataBinding {
        enabled = true
    }


    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            ndk {
                abiFilters "x86","armeabi" //默认只启用x86,armeabi
            }
        }
    }
}

////开启atlas容器功能
atlas {

    atlasEnabled true//atlasEnable字段需要指定为true才能开启打包阶段的基于容器扩展的task
//    tBuildConfig {
//        //引用了几个插件并没有配为自启动，那根据文档只能通过触发插件中Component的加载而启动插件，比如start一个bundle中的activity或者service。或者是通过手动调用代码加载一个插件(http://atlas.taobao.org/docs/guide-for-use/guide_for_bundle.html).
//        autoStartBundles = ['com.mobile.android.launcher'] //自启动bundle配置
//        outOfApkBundles = ['remotebundle']//不把bundle编译进APK，在客户端使用时下载后加载
//        preLaunch = 'com.mobile.android.launcher.taobao.demo.DemoPreLaunch'//AppApplication启动之前调用com.mobile.android.launcher.taobao.demo
//        classInject false
//        dataBindingBundles = ['com.taobao.databindbundle', 'com.mobile.android.launcher']
//    }
    tBuildConfig {
        //引用了几个插件并没有配为自启动，那根据文档只能通过触发插件中Component的加载而启动插件，比如start一个bundle中的activity或者service。或者是通过手动调用代码加载一个插件(http://atlas.taobao.org/docs/guide-for-use/guide_for_bundle.html).
        autoStartBundles = ['com.mobile.android.launcher'] //自启动bundle配置
        outOfApkBundles = ['remotebundle']//不把bundle编译进APK，在客户端使用时下载后加载
//        preLaunch = 'com.taobao.demo.DemoPreLaunch'//AppApplication启动之前调用
        classInject false
        dataBindingBundles = ['com.taobao.databindbundle', 'com.mobile.android.launcher']
    }

    manifestOptions {
        addAtlasProxyComponents true
    }


    patchConfigs {
        debug {
            createTPatch true
        }
    }


    buildTypes {
        debug {
            if (apVersion) {
                // 打差异补丁 gradlew assembleDebug -DapVersion=1.1.0 -DversionName=1.1.1
                // 对应着本地maven仓库地址 .m2/repository/com/android/dzatlas/AP-debug/1.1.4/AP-debug-1.1.4.ap
                baseApDependency "${group}:AP-debug:${apVersion}@ap"
                patchConfig patchConfigs.debug
            }
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile project(':middlewarelibrary')
    compile project(':splashscreen')
    compile project(':activitygroupcompat')
//
//    //业务bundle都是使用Atlas自定义的编译方法
    compile project(':android-framework-sdk-api')
    compile project(':android-framework-sdk-middle')
    bundleCompile project(':launcher')
    bundleCompile project(':firstbundle')
////    //从私有库
////    //bundleCompile("com.taobao.demo:firstbundle:1.0.2@awb")
    bundleCompile project(':secondbundle')
    bundleCompile project(':remotebundle')
    bundleCompile project(':publicbundle')
    bundleCompile project(':databindbundle')
}

String getEnvValue(key, defValue) {
    def val = System.getProperty(key);
    if (null != val) {
        return val;
    }
    val = System.getenv(key);
    if (null != val) {
        return val;
    }
    return defValue;
}

tasks.whenTaskAdded { task ->
    if (task.name.contains("DebugAndroidTest")) {
        task.setEnabled(false);
    }
    if (task.name.contains("assemble")) {
        def files = null;
        def file = new File(task.project.getBuildDir(), "outputs");
        if (file.exists() && new File(file, "tpatch-debug").exists()) {
            files = new File(file, "tpatch-debug").listFiles();
        }
        if (files != null) {
            for (File file1 : files) {
                if (file1.getName().endsWith(".json") || file1.getName().endsWith(".tpatch")) {

                    if (!new File(task.project.getRootDir(), "hisTpatch").exists()) {
                        new File(task.project.getRootDir(), "hisTpatch").mkdirs();
                    }
                    org.apache.commons.io.FileUtils.copyFileToDirectory(file1, new File(task.project.getRootDir(), "hisTpatch"));
                }
            }
        }
    }
}

apply plugin: 'maven'
apply plugin: 'maven-publish'

publishing {
    // 指定仓库位置
    repositories {
        mavenLocal()
    }
}

publishing {
    publications {
        // 默认本地仓库地址  /home/mi/xinming/Android/maven_localRepository
        maven(MavenPublication) {
            //读取ap目录上传maven
            artifact "${project.buildDir}/outputs/apk/${project.name}-debug.ap"
            //生成本地maven目录
            artifactId "AP-debug"//可以改
        }
    }
}
